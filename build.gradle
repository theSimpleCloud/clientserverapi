/*
 * MIT License
 *
 * Copyright (C) 2020 Frederick Baier
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
 * documentation files (the "Software"), to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
 * and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
 * ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'maven-publish'
    id 'maven'
}

group 'eu.thesimplecloud.clientserverapi'
version '5.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = "https://repo.thesimplecloud.eu/artifactory/list/gradle-release-local/"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.10.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.4.2'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.58.Final'
    compile group: 'commons-io', name: 'commons-io', version: '2.8.0'
    compile group: 'com.google.guava', name: 'guava', version: '30.0-jre'
    compile 'eu.thesimplecloud.jsonlib:json-lib:1.0.8'


}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            if (project.version.endsWith("SNAPSHOT")) {
                url 'https://repo.thesimplecloud.eu/artifactory/list/gradle-dev-local/'
            } else {
                url 'https://repo.thesimplecloud.eu/artifactory/list/gradle-release-local//'
            }

            credentials {
                username = project.hasProperty("mavenUser") ? project.property("mavenUser") : ""
                password = project.hasProperty("mavenPassword") ? project.property("mavenPassword") : ""
            }
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
